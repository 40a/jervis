apply plugin: 'groovy'
apply plugin: 'cobertura'
apply from: 'file:gradle/groovydoc.gradle'

group = 'net.gleske'
jar.baseName = 'jervis'
version = '0.1.0-SNAPSHOT'

ext { groovyVersion = System.getenv("GROOVY_VERSION")?: "1.8.9" }

println "Using Groovy ${project.groovyVersion}"

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.5"
        classpath "org.yaml:snakeyaml:1.14"
    }
}

repositories { mavenCentral() }
dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile 'org.yaml:snakeyaml:1.14'
    testCompile 'junit:junit:4.11'
    testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
    testCompile 'org.yaml:snakeyaml:1.14'
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main'
        }
        resources {
            srcDir 'src/resources'
        }
    }

    test {
        groovy {
            srcDir 'src/test'
        }
        resources {
            srcDirs = ['src/resources', 'src/testResources']
        }
    }
}

test {
  // show standard out and standard error of the test JVM(s) on the console
  testLogging.showStandardStreams = true

  // set heap size for the test JVM(s)
  minHeapSize = "128m"
  maxHeapSize = "512m"

  // set JVM arguments for the test JVM(s)
  jvmArgs '-XX:MaxPermSize=256m'

  // listen to events in the test execution lifecycle
  beforeTest { descriptor ->
     logger.lifecycle("Running test: " + descriptor)
  }

  // listen to standard out and standard error of the test JVM(s)
  onOutput { descriptor, event ->
     logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
  }
}

//http://jdpgrailsdev.github.io/blog/2014/04/29/gradle_cobertura.html
//https://github.com/stevesaliman/gradle-cobertura-plugin/blob/master/usage.md
cobertura {
    coverageFormats = ['html', 'xml']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
    coverageExcludes = ['.*package-info.*', '.*jervis\\.exceptions\\..*']

    //coberturaCheck values 80% (./gradlew coberturaCheck test)
    //coverageCheckBranchRate = 80
    //coverageCheckLineRate = 80
    coverageCheckPackageBranchRate = 80
    coverageCheckPackageLineRate = 80
    coverageCheckTotalBranchRate = 80
    coverageCheckTotalLineRate = 80
    coverageCheckHaltOnFailure = true

}
test.finalizedBy(project.tasks.cobertura)

//http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
//generate gradlew with: gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
